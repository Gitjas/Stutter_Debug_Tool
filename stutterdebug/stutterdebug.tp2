BACKUP ~weidu_external/backup/stutterdebug~
SUPPORT ~https://github.com/SpellholdStudios/Stutter_Debug_Tool~

VERSION ~4.0~

AUTO_TRA ~stutterdebug/tra/%s~


ALWAYS

  // required because of non-standard BACKUP path
  OUTER_TEXT_SPRINT MOD_FOLDER ~stutterdebug~
  // root folder for mutable data
  OUTER_TEXT_SPRINT WORK_FOLDER ~weidu_external~

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
    FAIL @100 // DLC Merger is required before mods can be installed on this game.
  END


  ACTION_DEFINE_ARRAY ~C#noconvert~ BEGIN ~setup~ END
  ACTION_DEFINE_ARRAY ~C#reload~    BEGIN ~mod~   END
  LAF HANDLE_CHARSETS
    INT_VAR
      from_utf8       = 1  // conversion: UTF8 -> ANSI
      infer_charsets  = 1
    STR_VAR
      tra_path        = EVAL ~%MOD_FOLDER%/tra~
      out_path        = EVAL ~%WORK_FOLDER%/lang/%MOD_FOLDER%~
      noconvert_array = ~C#noconvert~
      reload_array    = ~C#reload~
  END


  // List of broken scripts (BG1, non-bg2fixed BG2)
  ACTION_IF (GAME_IS ~bg1 totsc~ OR
             GAME_IS ~tutu tutu_totsc bg2 bgt tob~ AND NOT MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0") BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY broken_script BEGIN
      ~RDOG~      => 1
      ~RDWARF~    => 1
      ~RETTER~    => 1
      ~RGIBBLER~  => 1
      ~RHALFLIN~  => 1
      ~RHOBGOBA~  => 1
      ~RHOBGOBF~  => 1
      ~RKOBOLD~   => 1
      ~ROGRE~     => 1
      ~RSIREN~    => 1
      ~RSIRINE~   => 1
    END
  END

  // List of broken scripts (IWD2)
  ACTION_IF (GAME_IS ~iwd2~) BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY broken_script BEGIN
      ~TESTOCLE~  => 1
      ~TEST_01~   => 1
      ~TEST_02~   => 1
      ~TEST_03~   => 1
    END
  END

  INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

END


LANGUAGE ~Deutsch~
         ~german~   
         ~stutterdebug/tra/german/setup.tra~
         ~stutterdebug/tra/german/mod.tra~

LANGUAGE ~English~
         ~english~  
         ~stutterdebug/tra/english/setup.tra~ 
         ~stutterdebug/tra/english/mod.tra~ 


BEGIN @0 /* ~Patching all scripts except NoAction() blocks - chose this when installing the first time (Installation could take a while!)~ */

SUBCOMPONENT @2 /* ~Stutter Debug Tool~ */
DESIGNATED 1

LAF SHOW_GAME_INFO END

COPY_EXISTING_REGEXP GLOB ~.+\.bcs~ ~override~
  TO_UPPER ~SOURCE_RES~
  PATCH_IF (NOT VARIABLE_IS_SET $broken_script(~%SOURCE_RES%~)) BEGIN
    LPF patch_script INT_VAR patch_all = 0 END
  END
BUT_ONLY


BEGIN @1 /* ~Patching all scripts and scriptblocks - this will take a while!~ */
SUBCOMPONENT @2 /* ~Stutter Debug Tool~ */
DESIGNATED 2

LAF SHOW_GAME_INFO END

COPY_EXISTING_REGEXP GLOB ~.+\.bcs~ ~override~ 
  TO_UPPER ~SOURCE_RES~
  PATCH_IF (NOT VARIABLE_IS_SET $broken_script(~%SOURCE_RES%~)) BEGIN
    LPF patch_script INT_VAR patch_all = 1 END
  END
BUT_ONLY
